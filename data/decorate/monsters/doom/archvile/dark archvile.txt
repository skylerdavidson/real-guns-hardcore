//Cheers to whoever made the black Archvile from insanmons.wad
actor DarkArchvile : Archvile_2
{
  obituary "%o was obliterated by a Dark archvile."
  health 1200
  radius 20
  height 56
  cameraheight 48
  mass 500
  speed 15
  painchance 10
  seesound "vile2/sight"
  painsound "vile2/pain"
  deathsound "vile2/death"
  activesound "vile/active"
  MONSTER
  +FLOORCLIP
  +NOTARGET
  +QUICKTORETALIATE
  MaxTargetRange 896
  +GHOST
  damagefactor "Archvile", 0.0
  DropItem "Soulsphere2" 96
  states
  {
  Spawn:
    POSS A 0 
    POSS A 0 Thing_SetSpecial(0,226,613,0,random(100,150))
    POSS A 0 A_GiveInventory("XPAmount", random(100, 150))
    DVIL AB 10 A_Look
    goto Spawn+3
  See:
    DVIL AABBCCDDEEFF 2 A_VileChase
    loop
  Missile:
    DVIL G 0 bright A_PlaySound("vile/start")
    DVIL G 5 bright A_FaceTarget
    DVIL H 5 bright A_FaceTarget
    DVIL H 0 bright A_PlaySound("bfg/start")
    DVIL IJKLMN 5 bright A_FaceTarget
    DVIL O 5 bright A_CustomMissile("DarkArchvileBFG",48,-8)
    DVIL P 15 bright
    goto See
  Heal:
    DVIL H 0 bright
    DVIL GGGGGH 2 bright A_SpawnItemEx("DarkArchvileHealer",0,0,0,0,0,0,0,32,0)
    DVIL H 6 bright
    DVIL IHG 6 bright
    goto See
  Pain:
    DVIL Q 5
    DVIL Q 5 A_Pain
	TNT1 G 0 A_AlertMonsters
    goto See
  Death:
    DVIL Q 20 A_NoBlocking
    DVIL Q 0 ACS_ExecuteAlways(648,0,0,0,0)
    DVIL R 0 A_CustomMissile("KamikazeExplosion",16,0,random(-180,180),2,random(-36,36))
    DVIL S 0 A_SpawnItemEx("Archvile_2",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TELEFRAG,0)
    TNT1 A -1
    stop
  Raise:
    VILE O 0
    stop
  }
}

actor DarkArchvileBFG
{
  RenderStyle Add
  alpha 0.95
  radius 8
  height 8
  speed 16
  damage (25)
  seesound "bfg/fire"
  deathsound "bfg/explode1"
  PROJECTILE
  +EXTREMEDEATH 
  +FORCERADIUSDMG
  +SKYEXPLODE
  decal BFGLightning
  scale 0.2
  DamageType "Archvile"
  states
  {
  Spawn:
    BFG2 A 1 BRIGHT
    BFG2 A 0 BRIGHT
    BFG2 A 9 BRIGHT A_Explode(2,512,0)
    loop
  Death:
    TNT1 A 0 A_RadiusThrust(160,128)
    TNT1 A 0 Radius_Quake(3,30,0,128,0)
    TNT1 A 0 A_SpawnItemEx("BFG20kExplosionSmall", 0, 0, 0, 0, 0, 0, 0, 0, 0)
    TNT1 A 5 A_Explode(25,512,0)
    stop
  }
}

actor DarkArchvileHealer
{
  health 99999
  radius 256
  height 32
  mass 500
  speed 20
  painchance 0
  MONSTER
  -SHOOTABLE
  -NOCLIP
  -NOBLOCKMAP
  -COUNTKILL
  -SOLID
  +NOTARGET
  +LOOKALLAROUND
  Renderstyle None
  states
  {
  Spawn:
    TNT1 AA 4 A_Look
    Goto See
  See:
    TNT1 AAAAAA 4 A_VileChase
    stop
  Heal:
    TNT1 AA 5 bright
    TNT1 A 5
    stop
  }
}

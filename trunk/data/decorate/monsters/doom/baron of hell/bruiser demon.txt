ACTOR BruiserDemon : RGAMonster
{
	Tag "Bruiser Demon"
	//$Category Monsters
	Health 1100
	Radius 24
	Height 64
	cameraheight 54
	Mass 2000
	Speed 8
	PainChance 15
	Monster
	+BOSS
	+FLOORCLIP
	+MISSILEEVENMORE
	+NORADIUSDMG
	MissileType BruiserBall
	SeeSound "superbaron/scream"
	PainSound "superbaron/pain"
	DeathSound "superbaron/death"
	ActiveSound "superbaron/act"
	MeleeSound "baron/melee"
	Obituary "%o was slaughtered by a Bruiser Demon."
	HitObituary "%o was cremated by a Bruiser Demon."
	MeleeDamage 20
	Scale 1.00
	DropItem "SoulSphere2" 128
	States
	{
	Spawn:
		BRS2 A 0
		BRS2 A 0 Thing_SetSpecial(0,226,613,0,random(60,80))
		POSS A 0 A_GiveInventory("XPAmount", random(40, 60))
		POSS P 0 ACS_ExecuteAlways(651,0,0,0,0)
		BRS2 AB 10 Bright A_Look
		goto Spawn+4
	See:
		BRS2 AABBCCDD 3 Bright A_Chase
		Loop
	Melee:
		BRS2 E 6 Bright A_FaceTarget
		BRS2 F 6 Bright A_FaceTarget
		BRS2 G 6 Bright A_ComboAttack
		Goto See
	Missile:
		BRS2 E 0 Bright A_Jump(144, 8)
		BRS2 EF 6 Bright A_FaceTarget
		BRS2 G 6 Bright A_MissileAttack
		BRS2 G 0 Bright A_Jump(96, 1)
		Goto See
		BRS2 HI 6 Bright A_FaceTarget
		BRS2 J 6 Bright A_MissileAttack
		Goto See
		BRS2 E 0 Bright A_Jump(96, 20)
		BRS2 E 6 Bright A_FaceTarget
		BRS2 F 4 Bright A_FaceTarget
		BRS2 F 2 Bright A_Custommissile("BruiserBall2",56,0,-40,0)
		BRS2 G 1 Bright A_Custommissile("BruiserBall2",56,0,-30,0)
		BRS2 G 1 Bright A_Custommissile("BruiserBall2",56,0,-20,0)
		BRS2 G 1 Bright A_Custommissile("BruiserBall2",56,0,-10,0)
		BRS2 G 1 Bright A_Custommissile("BruiserBall2",56,0,0,0)
		BRS2 G 1 Bright A_Custommissile("BruiserBall2",56,0,10,0)
		BRS2 G 1 Bright A_Custommissile("BruiserBall2",56,0,20,0)
		BRS2 G 0 Bright A_Jump(96, 1)
		Goto See
		BRS2 H 6 Bright A_FaceTarget
		BRS2 I 4 Bright A_FaceTarget
		BRS2 I 2 Bright A_Custommissile("BruiserBall2",56,0,40,0)
		BRS2 J 1 Bright A_Custommissile("BruiserBall2",56,0,30,0)
		BRS2 J 1 Bright A_Custommissile("BruiserBall2",56,0,20,0)
		BRS2 J 1 Bright A_Custommissile("BruiserBall2",56,0,10,0)
		BRS2 J 1 Bright A_Custommissile("BruiserBall2",56,0,0,0)
		BRS2 J 1 Bright A_Custommissile("BruiserBall2",56,0,-10,0)
		BRS2 J 1 Bright A_Custommissile("BruiserBall2",56,0,-20,0)
		Goto See
		BRS2 KL 6 Bright A_FaceTarget
		BRS2 M 2 Bright A_Custommissile("BruiserFireSpawner",16,0,-15,0)
		BRS2 M 2 Bright A_Custommissile("BruiserFireSpawner",16,0,0,0)
		BRS2 M 2 Bright A_Custommissile("BruiserFireSpawner",16,0,15,0)
		Goto See
	Pain:
		BRS2 N 5 Bright A_Pain
 		Goto See
	Death:
		BRUD A 6 Bright A_Scream
		BRUD BCD 4 Bright 
		BRUD EFG 4 Bright
		BRUD H 4 Bright A_Fall
		BRUD H 0 ACS_ExecuteAlways(648,0,0,0,0)
		BRUD IJKLMNOP 4 Bright
		BRUD QRSTUV 4
		BRUD W 0 A_BossDeath
		BRUD W -1 		  
		Stop
	}
}

ACTOR BruiserBall
{
	Radius 16
	Height 16
	Speed 18
	Damage 16
	Projectile
	+RANDOMIZE
	RenderStyle "ADD"
	Alpha 0.9
	SeeSound "imp/attack"
	DeathSound "imp/shotx"
	Decal Scorch
	States
	{
	Spawn: 
		BRBA AABB 2 BRIGHT A_SpawnItemEx("BruiserBallTrail", 0, 0, 0, 0, 0, 0, 0, 128)
		Loop
	Death:
		BRBA KLMNOPQRSTUVWX 3 Bright
		Stop
	}
}

ACTOR BruiserBall2
{
	Radius 8
	Height 8
	Speed 10
	Damage 8
	Projectile
	+RANDOMIZE
	RenderStyle "ADD"
	Alpha 0.9
	SeeSound "imp/attack"
	DeathSound "imp/shotx"
	Decal BaronScorch
	States
	{
	Spawn: 
		BRB2 AB 6 BRIGHT 
		Loop
	Death:
		BRB2 CDEFGHI 3 Bright
		Stop
	}
}

ACTOR BruiserFireSpawner
{
	Radius 8
	Height 8
	Speed 12
	Damage 0
	+RIPPER
	+FLOORHUGGER
	+BLOODLESSIMPACT
	Projectile
	MissileType BruiserFire
	Missileheight 0
	States
	{
	Spawn: 
		TNT1 A 3 Bright A_MissileAttack
  		TNT1 A 3
  		TNT1 A 3
		Loop
	Death:	
		TNT1 A 6
		Stop
	}
}

actor BruiserBallTrail
{
    Radius 1
    Height 1
    Speed 0
    PROJECTILE
    RENDERSTYLE ADD
    ALPHA 0.70
    +NOCLIP
    States
    {
    Spawn:
     	BRBA CDEFGHIJ 4 Bright
     	Stop       
    }
}

ACTOR BruiserFire
{
	Radius 1
    	Height 1
    	Speed 0
    	Damage 20
    	PROJECTILE
    	RENDERSTYLE ADD
    	ALPHA 0.9
    	+NOCLIP
	+FLOORHUGGER
	SeeSound "weapons/rocklx"
    	States
    	{
    	Spawn:
  		XXBF AB 3 Bright
    		XXBF C 3 Bright A_Explode(48,128,0)
		XXBF DEFGHIJKLMNOPQRST 3 Bright
     		Stop       
   	}
}

